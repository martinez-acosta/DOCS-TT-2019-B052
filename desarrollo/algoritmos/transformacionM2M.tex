\subsection{Transformación de una instancia GDM al modelo lógico orientado a documentos}
De acuerdo con la sección~\ref{alg:gdm-to-logic} y la sección~\ref{sec:xtend}, se implementará el algoritmo usando Xtend.

Cualquier archivo de texto con una definición correcta del lenguaje GDM será convertido por Mortadelo a un modelo orientado a documentos.


Como se ha explicado en la sección~\ref{alg:gdm-to-logic}, el algoritmo a implementar es una transformación entre modelos. A continuación se adjunta una breve descripción del algoritmo y su implementación en Mortadelo.

\begin{algorithm}[H]
    \SetKwInOut{Input}{Input}
    \SetKwInOut{Output}{Output}

    %\underline{function Euclid} $(a,b)$\;
    \Input{una instancia del modelo GDM, $gdm$}
    \Output{un modelo lógico orientado a documentos, $ddm$}
    $mainEntities \gets gdm.queries.collect((q)|q.from);$\\
    \ForEach{$me \in mainEntities$}{
        $collection \gets new Collection();$\\
        $collection.name \gets me.name;$\\
        $accessTree \gets allQueryPaths(me,gdm.queries);$\\
        $collection \gets populateDocumentType(collection.root,accessTree);$\\
        $ddm.collections.add(collection);$
    }
    
    \caption{Transformación del modelo conceptual GDM al modelo lógico orientado a documentos}
\end{algorithm}

Donde la función populateDocumentType() es otro algoritmo de la forma:

\begin{algorithm}[H]
    \SetKwInOut{Input}{Input}
    \SetKwInOut{Output}{Output}

    %\underline{function Euclid} $(a,b)$\;
    \Input{Un ``document type", $dt$}
    \Output{un nodo del arbol de acceso}
    $ nodeAttributes \gets node.entity.features.select(f|f.isTypeOf(Attribute))\; $\\
    $ nodeReferences \gets node.entity.features.select(f|f.isTypeOf(Reference))\; $\\
    \ForEach{$attr \in nodeAttributes$}{
        $pf \gets new PrimitiveField()\;$\\
        $pf.name \gets attr.name\;$\\
        $pf.type \gets attr.type\;$\\
        $dt.fields.add(pf)\;$\\
    }
    \ForEach{$ref \in nodeReferences$}{
        $targetNode \gets node.arcs.find(a|a.name = ref.name).target\;$\\
        \uIf{exists(targetNode)}{
            $baseType \gets new DocumentType()\;$\\
            $populateDocumentType(baseType,targetNode)\;$\\
        }
        \Else{
            $baseType \gets new PrimitiveField()\;$\\
            $baseType.type \gets findIdType(ref.entity)\;$\\
        }
        $baseType.name \gets ref.name\;$\\

        \uIf{$ref.cardinality$ == 1}{
            $dt.field.add(baseType)\;$\\
            
        }
        \Else{
            $arrayField \gets new ArrayField()\;$\\
            $arrayField.type \gets baseType\;$\\
            $dt.fields.add(arrayField)\;$
        }
        
    }
    \caption{Generar el contenido de un \textit{DocumentType} dado un árbol de acceso}
\end{algorithm}

\begin{code}
    \captionof{listing}{Transformación modelo a modelo: GDM a modelo lógico orientado a documentos}
    \label{code:mortadelo-gdm-m2m}
    \begin{minted}[linenos,tabsize=2,breaklines, fontsize=\small]{java}

package es.unican.istr.mortadelo.gdm.transformations.gdm2document

import columnFamilyDataModel.ColumnFamilyDataModelPackage
import documentDataModel.Document
import documentDataModel.DocumentDataModel
import documentDataModel.DocumentDataModelFactory
import documentDataModel.Field
import documentDataModel.PrimitiveField
import documentDataModel.PrimitiveType
import es.unican.istr.mortadelo.gdm.lang.gdmLang.Attribute
import es.unican.istr.mortadelo.gdm.lang.gdmLang.Entity
import es.unican.istr.mortadelo.gdm.lang.gdmLang.GdmLangPackage
import es.unican.istr.mortadelo.gdm.lang.gdmLang.Model
import es.unican.istr.mortadelo.gdm.lang.gdmLang.Query
import es.unican.istr.mortadelo.gdm.lang.gdmLang.Reference
import es.unican.istr.mortadelo.gdm.lang.gdmLang.Type
import es.unican.istr.mortadelo.gdm.transformations.common.Tree
import java.io.File
import java.io.IOException
import java.util.ArrayList
import java.util.Collection
import java.util.Collections
import java.util.List
import java.util.Set
import org.eclipse.emf.common.util.URI
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.emf.ecore.resource.impl.ResourceSetImpl
import org.eclipse.emf.ecore.xmi.impl.XMIResourceFactoryImpl

class Gdm2Document {

  // Test the transformation below
  def static void main(String[] args) {
    val example = "eCommerce"
    println("GDM to document logical model")
    println(String.format("Example: %s", example))
    val totalStart = System.currentTimeMillis()
    // Prepare the xmi resource persistence
    val Resource.Factory.Registry reg = Resource.Factory.Registry.INSTANCE
    val m = reg.extensionToFactoryMap
    m.put("model", new XMIResourceFactoryImpl())
    // Initialize models
    GdmLangPackage.eINSTANCE.eClass
    ColumnFamilyDataModelPackage.eINSTANCE.eClass
    // Load the GDM model
    val input = new File(
      String.format("../es.unican.istr.mortadelo.gdm.examples/%s.model",
                    example))
    val resSet = new ResourceSetImpl()
    val inputResource = resSet.getResource(URI.createURI(input.canonicalPath),
                                           true)
    val gdm = inputResource.contents.get(0) as Model
    // Transform it
    val start = System.currentTimeMillis()
    val columnFamilyDM = transformGdm2Document(gdm)
    val end = System.currentTimeMillis()
    // Save the obtained data model
    val output = new File(
      String.format("../es.unican.istr.mortadelo.gdm.examples/document/%sDOC.model",
                    example))
    val outputResource = resSet.createResource(
      URI.createURI(output.canonicalPath))
    outputResource.getContents().add(columnFamilyDM)
    try {
      outputResource.save(Collections.EMPTY_MAP)
    } catch (IOException e) {
      e.printStackTrace()
    }
    val totalEnd = System.currentTimeMillis()
    println("Transformation finished")
    println(String.format("Transformation time: %d ms", end - start))
    println(String.format("Total time (read/write files and models): %d ms",
        totalEnd - totalStart))
  }

  def static DocumentDataModel transformGdm2Document(Model gdm) {
    val docFactory = DocumentDataModelFactory.eINSTANCE
    val DocumentDataModel docModel = docFactory.createDocumentDataModel()
    // obtain main entities (those appearing in a from clause) and their queries
    val mainEntities = gdm.queries.map[q | q.from.entity].toSet
    val entityToQueries =  mainEntities.map[me |
      me -> gdm.queries.filter[q | q.from.entity.equals(me)]]
    // generate access trees
    val entity2accessTree = newImmutableMap(
      entityToQueries.map[e2q |
                          e2q.key -> createAccessTree(e2q.value)])
    // complete each access tree with the information of the rest
    for (entity : mainEntities) {
      val tree = entity2accessTree.get(entity)
      val otherTrees = new ArrayList<Tree<Reference>> (entity2accessTree.values)
      otherTrees.remove(tree)
      completeAccessTree(entity, tree, otherTrees)
    }
    // collections generation
    for (entity : mainEntities) {
      val tree = entity2accessTree.get(entity)
      val newCol = docFactory.createCollection()
      newCol.name = entity.name
      val docType = docFactory.createDocument()
      docType.name = "root"
      newCol.root = docType
      populateDocument(docType, docFactory, entity, tree, mainEntities)
      docModel.collections.add(newCol)
    }
    return docModel
  }

  /**
   *  Obtains the access tree of an entity, including the reference traversed
   *  for each step of the path
   *
   *  @returns A tree of pairs (traversed reference, destination entity)
   */
  def private static Tree<Reference> createAccessTree(Iterable<Query> queries) {
    // root element of the tree has no reference
    val tree = new Tree<Reference> (null)
    for (query : queries) {
      for (inclusion : query.inclusions) {
        // populate the tree with all the concatenated references
        // the add method of the tree handles collisions for us
        var auxTree = tree
        for (ref : inclusion.refs) {
          val child = auxTree.add(ref)
          auxTree = child // we keep adding children down the rabbit hole
        }
      }
    }
    return tree
  }

  def private static void completeAccessTree(Entity entity, Tree<Reference> tree,
      Collection<Tree<Reference>> otherTrees) {
    for (oTree : otherTrees) {
      val treeNodes = searchEntity(entity, oTree)
      for (treeNode : treeNodes) {
        addTreeNode(tree, treeNode)
      }
    }
  }

  def private static List<Tree<Reference>> searchEntity(Entity entity,
      Tree<Reference> tree) {
    val treeNodes = new ArrayList<Tree<Reference>>()
    for (child : tree.children) {
      if (child.data.entity.equals(entity)) {
        treeNodes.add(child)
      }
      treeNodes.addAll(searchEntity(entity, child))
    }
    return treeNodes
  }

  def private static void addTreeNode(Tree<Reference> tree, Tree<Reference> treeNode) {
    for (child : treeNode.children) {
      val addedChild = tree.add(child.data)
      addTreeNode(addedChild, child)
    }
  }

  def private static void populateDocument(Document document,
      DocumentDataModelFactory docFactory, Entity entity, Tree<Reference> tree,
      Set<Entity> mainEntities) {
    addAttributes(document, entity, docFactory)
    for (child : tree.children) {
      val reference = child.data
      // main entity pruning: if reference points to a main entity, instead of
      //   generating the document we create a reference
      var Field newField = null
      if (mainEntities.contains(reference.entity)) {
        newField = docFactory.createPrimitiveField()
        newField.name = reference.entity.name.toFirstLower + "Ref"
        (newField as PrimitiveField).type = PrimitiveType.ID
      } else {
        newField = docFactory.createDocument()
        newField.name = reference.entity.name.toFirstLower
        populateDocument(newField as Document, docFactory, reference.entity,
          child, mainEntities)
      }
      if (!reference.cardinality.equals("1")) {
        // encapsulate field in an array
        val arrayField = docFactory.createArrayField()
        arrayField.name = newField.name + "Array"
        arrayField.type = newField
        document.fields.add(arrayField)
      } else {
        document.fields.add(newField)
      }
    }
  }

  def private static void addAttributes(Document document, Entity entity,
      DocumentDataModelFactory docFactory) {
    for (attr : entity.features.filter[f | f instanceof Attribute]) {
      val field = docFactory.createPrimitiveField()
      field.name = attr.name
      field.type = convertType((attr as Attribute).type)
      document.fields.add(field)
    }
  }

  def private static PrimitiveType convertType(Type type) {
    switch(type) {
      case ID:
        return PrimitiveType.ID
      case NUMBER:
        return PrimitiveType.FLOAT
      case DATE:
        return PrimitiveType.DATE
      case TIMESTAMP:
        return PrimitiveType.TIMESTAMP
      case BOOLEAN:
        return PrimitiveType.BOOLEAN
      default:
        return PrimitiveType.TEXT
    }
  }
}

\end{minted}
\end{code}

La implementación del código fuente~\ref{code:mortadelo-gdm-m2m} toma de entrada una instancia del modelo GDM en XML (un .model generado automaticamente por el \textit{framerok de Eclipse Modeling Framework}), y de salida genera otro .model correspondiente al modelo lógico orientado a documentos.

Obtiene las entidades principales de las consultas y por cada entidad se genera una nueva colección con un nombre. Después se hace una búsqueda transversal del árbol de consutlas con cada \textit{query} y se genera el documento raíz del modelo lógico orientado a documentos recursivamente hasta que no haya más colecciones anidadas a añadir.