\subsection{Transformación modelo entidad-relación con consultas a una instancia GDM}
Como se ha mencionado en la sección, debido a que está fuera del alcance de un alumno de ingeneiría desarrollar un compilador para poder hacer la transformación modelo a modelo, se hace uso de la implementación de Mortadelo en Java. 


A continuación se explica brevente la implementación.

\begin{code}
    \captionof{listing}{Gramática del Generic Data Model}
    \label{code:mortadelo-gdm-grammar}
    \begin{minted}[linenos,tabsize=2,breaklines, fontsize=\small]{java}
grammar es.unican.istr.mortadelo.gdm.lang.GdmLang
    with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate gdmLang "http://www.unican.es/istr/mortadelo/gdm/lang/GdmLang"

Model:
  (entities+=Entity)*
  (queries+=Query)*;

AnnotatableElement:
  Feature | Entity | Query
;

Annotation:
  "@"name=ID
;

Entity:
  (annotations+=Annotation (annotations+=Annotation)*)?
  "entity" name=ID "{"
  (features+=Feature)*
  "}";

Feature:
  Attribute | Reference;

Attribute:
  (annotations+=Annotation (annotations+=Annotation)*)?
  (type=Type)?("[" cardinality=CARDINALITY "]")? name=ID (unique?="unique")?;

Reference:
  (annotations+=Annotation (annotations+=Annotation)*)?
  "ref" entity=[Entity]
  "[" cardinality=CARDINALITY "]"
  name=ID;

terminal CARDINALITY returns ecore::EString:
  "*" | ('1'..'9') ('0'..'9')*;

enum Type:
  NUMBER="number" | TEXT="text" | DATE="date" | BOOLEAN="boolean" | ID="id" |
  TIMESTAMP="timestamp";

Query:
  (annotations+=Annotation (annotations+=Annotation)*)?
  "query" name=ID ":"
  "select" projections+=AttributeSelection ("," projections+=AttributeSelection)*
  "from" from=From
  ("including" inclusions+=Inclusion ("," inclusions+=Inclusion)*)?
  ("where" condition=BooleanExpression)?
  ("order" "by" orderingAttributes+=AttributeSelection
      ("," orderingAttributes+=AttributeSelection)*)?;

AttributeSelection:
  refAlias=[Alias] "." attribute=[Attribute] ("as" alias=ID)?;

Alias:
  "as" name=ID;

From:
  entity=[Entity] alias=Alias;

Inclusion:
  refAlias=[Alias] ("." refs+=[Reference])+ alias=Alias;

BooleanExpression returns BooleanExpression:
  AndConjunction;

AndConjunction returns BooleanExpression:
  OrConjunction ({AndConjunction.left=current} "and" right=OrConjunction)*;

OrConjunction returns BooleanExpression:
  Primary ({OrConjunction.left=current} "or" right=Primary)*;

Primary returns BooleanExpression:
  Comparison | '(' AndConjunction ')';

Comparison:
  {Equality}
  selection=AttributeSelection '=' value=STRING |
  {Inequality}
  selection=AttributeSelection '!=' value=STRING |
  {MoreThan}
  selection=AttributeSelection '>' value=STRING |
  {MoreThanOrEqual}
  selection=AttributeSelection '>=' value=STRING |
  {LessThan}
  selection=AttributeSelection '<' value=STRING |
  {LessThanOrEqual}
  selection=AttributeSelection '<=' value=STRING;
\end{minted}
\end{code}

La gramática GDM del código~\ref{code:mortadelo-gdm-grammar} está compuesta por entidades y consultas, cada entidad contiene \textit{features}, y cada \textit{feature} puede ser atributo o una referencia a otra entidad. Todos los atributos tienen un tipo y una cardinalidad,
